apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.squareup.sqldelight'

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.squareup.sqldelight:gradle-plugin:1.2.2'
    }
}

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        consumerProguardFiles "proguard-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

sqldelight {
    AppDB {
        //package name used for the generated MyDatabase.kt
        packageName = "com.pr0gramm.app.db"

        // The directory where to store '.db' schema files relative to the root of the project.
        // These files are used to verify that migrations yield a database with the latest schema.
        // Defaults to null so the verification tasks will not be created.
        schemaOutputDirectory = file("src/main/sqldelight/databases")
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.71"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"

    implementation "com.squareup.moshi:moshi:1.9.2"
    implementation "com.squareup.retrofit2:retrofit:2.8.1"
    implementation "com.squareup.okhttp3:okhttp:4.4.1"

    api "com.squareup.sqldelight:android-driver:1.2.2"
    api "com.squareup.sqldelight:coroutines-extensions-jvm:1.2.2"

    // 1.9.2 generates code that does not work with r8 full mode
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all { task ->
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    kotlinOptions {
        freeCompilerArgs += [
                "-Xuse-experimental=kotlin.contracts.ExperimentalContracts",
                "-XXLanguage:+InlineClasses",
        ]

        if (task.name == "compileReleaseKotlin") {
            freeCompilerArgs += [
                    "-Xno-call-assertions",
                    "-Xno-param-assertions",
                    "-Xno-receiver-assertions",
            ]
        }
    }
}
