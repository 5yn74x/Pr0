apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

apply from: 'version.gradle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'io.fabric.tools:gradle:1.19.2'
        classpath 'me.tatarka:gradle-retrolambda:3.2.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.6'
        classpath 'net.orfjackal.retrolambda:retrolambda:2.0.4'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22

        versionCode appVersion
        versionName "1.${(int)(appVersion / 10)}"

        resConfigs "en", "de"
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    signingConfigs {
        open {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        play {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "playstore"
            keyPassword "so very secret"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        def flavorSigningOpen = signingConfigs.open
        def flavorSigningPlay = signingConfigs.play

        open {
            applicationId "com.pr0gramm.app"
            signingConfig flavorSigningOpen
            buildConfigField "boolean", "IS_PLAYSTORE_RELEASE", "false"
        }

        play {
            applicationId "io.github.mopsalarm.pr0gramm.gp"
            signingConfig flavorSigningPlay
            buildConfigField "boolean", "IS_PLAYSTORE_RELEASE", "true"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        def filesToDelete = fileTree(variant.mergeResources.outputDir) {
            // remove timezone data
            include "raw/joda_time_android_tzdata_*"
            exclude "raw/joda_time_android_tzdata_etc_utc"
            exclude "raw/joda_time_android_tzdata_europe_berlin"
            exclude "raw/joda_time_android_tzdata_zoneinfomap"

            // remove left-to-right drawables
            include "drawable-ldrtl-*/*"
        }

        filesToDelete.each { it.delete() }
    }
}

retrolambda {
    defaultMethods true
}

repositories {
    maven { url "https://maven.fabric.io/public" }
    maven { url "http://bright.github.io/maven-repo/" }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.google.guava:guava:18.0'

    compile 'net.danlew:android.joda:2.8.1'
    compile 'org.roboguice:roboguice:3.0.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'io.reactivex:rxandroid:0.25.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    compile 'com.github.satyan:sugar:1.3.1'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.2.2'
    compile 'com.github.tony19:named-regexp:0.2.3'
    compile 'com.pnikosis:materialish-progress:1.5'
    compile 'de.psdev.licensesdialog:licensesdialog:1.7.0'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.google.android.apps.muzei:muzei-api:2.0'

    compile('pl.brightinventions:slf4android:0.1.1@aar') {
        transitive = true
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }

    // find all the memory leaks!
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    apt 'org.roboguice:roboblender:3.0.1'
    apt 'org.immutables:value:2.0.16'
    provided 'org.immutables:value:2.0.16'
    provided 'org.immutables:gson:2.0.16'
}
