apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

apply from: 'version.gradle'

buildscript {
    ext {
        VersionKotlin = "1.1.1"
        VersionSupportLibrary = "25.3.1"
        VersionRxBinding = "1.0.1"
        VersionImmutables = "2.4.6"
        VersionRetrofit = "2.2.0"
        VersionGoogleServices = "10.2.1"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
        classpath 'io.fabric.tools:gradle:1.22.1'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$VersionKotlin"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        applicationId "com.pr0gramm.app"

        versionCode appVersion
        versionName "1.${(int) (appVersion / 10)}.${(int) (appVersion % 10)}"

        resConfigs "en", "de"

        // vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "x86", "x86_64", "armeabi", "armeabi-v7a", "arm64-v8a"
        }

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    aaptOptions {
        // See https://www.reddit.com/r/androiddev/comments/47ni7v/if_you_support_older_versions_of_android_there_is/
        additionalParameters "--no-version-vectors"
        cruncherEnabled = false
    }

    sourceSets.main {
        java.srcDirs += 'src/main/kotlin'
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules-release.pro', getDefaultProguardFile('proguard-android-optimize.txt')

            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        def filesToDelete = fileTree(variant.mergeResources.outputDir) {
            // remove timezone data
            include "raw/joda_*"
            exclude "raw/joda_etc_utc"
            exclude "raw/joda_europe_berlin"
            exclude "raw/joda_europe_zurich"
            exclude "raw/joda_zoneinfomap"

            // remove left-to-right drawables
            include "drawable-ldrtl-*/*"
        }

        filesToDelete.each { it.delete() }
    }

    // delete .so-files we dont want in the finished build
    tasks."merge${variant.name.capitalize()}JniLibFolders".doLast {
        def filesToDelete = fileTree("build/") {
            include "**/armeabi/libpl_droidsonroids_gif.so"
            include "**/mips*/*.so"
        }

        filesToDelete.each { it.delete() }
    }
}

retrolambda {
    incremental true
}


repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://maven.fabric.io/public" }
    maven { url "http://bright.github.io/maven-repo/" }
    maven { url "http://dl.bintray.com/amulyakhare/maven" }
    maven { url "http://dl.bintray.com/skoumal/maven" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile project(":api")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$VersionKotlin"
    compile "com.google.code.findbugs:jsr305:3.0.2"

    compile "com.android.support:support-fragment:$VersionSupportLibrary"
    compile "com.android.support:recyclerview-v7:$VersionSupportLibrary"
    compile "com.android.support:design:$VersionSupportLibrary"
    compile "com.android.support:customtabs:$VersionSupportLibrary"
    compile "com.android.support:palette-v7:$VersionSupportLibrary"
    compile "com.android.support:support-annotations:$VersionSupportLibrary"
    compile "com.android.support:exifinterface:$VersionSupportLibrary"
    compile("com.android.support:appcompat-v7:$VersionSupportLibrary") {
        exclude(module: "support-media-compat")
        exclude(module: "support-core-utils")
    }

    compile("com.google.guava:guava:20.0") {
        force = true
    }

    compile "com.google.android.gms:play-services-analytics:$VersionGoogleServices"
    compile "com.google.android.gms:play-services-ads:$VersionGoogleServices"

    compile "net.danlew:android.joda:2.9.9"
    compile "com.squareup.retrofit2:retrofit:$VersionRetrofit"
    compile "com.squareup.retrofit2:converter-gson:$VersionRetrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$VersionRetrofit"
    compile "com.squareup.okhttp3:okhttp:3.6.0"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
    compile "pl.droidsonroids.gif:android-gif-drawable:1.2.6"
    compile "com.github.tony19:named-regexp:0.2.4"
    compile "com.pnikosis:materialish-progress:1.7"
    compile "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"
    compile "com.squareup.sqlbrite:sqlbrite:1.1.1"
    compile "com.infstory:proguard-annotations:1.0.2"
    compile "net.sf.trove4j:trove4j:3.0.3"
    compile "com.davemorrissey.labs:subsampling-scale-image-view:3.6.0"
    compile "org.nanohttpd:nanohttpd:2.3.1"
    compile "com.indeed:java-dogstatsd-client:2.0.16"
    compile "me.leolin:ShortcutBadger:1.1.14@aar"

    // compile 'com.evernote:android-job:1.1.8'
    compile "com.github.evernote:android-job:cdb9d51ba25260fd4294154e70da0657bffd448d"

    // a little bit more recent then 4.1.0
    compile "com.github.paolorotolo:appintro:7cffb7c40744b0027729aa86995accd839a80a02"

    compile "com.google.android.exoplayer:exoplayer:r2.3.1"

    // forked dependencies.
    compile "com.github.mopsalarm:FinestWebView-Android:aef2e77ca9"

    debugCompile "com.facebook.stetho:stetho:1.4.2"
    debugCompile "com.facebook.stetho:stetho-okhttp3:1.4.2"
    apt "com.facebook.stetho:stetho:1.4.2"
    apt "com.facebook.stetho:stetho-okhttp3:1.4.2"

    compile "com.jakewharton:butterknife:8.5.1"
    compile "com.jakewharton:butterknife-annotations:8.5.1"
    apt "com.jakewharton:butterknife-compiler:8.5.1"

    compile "com.f2prateek.dart:dart:2.0.2"
    apt "com.f2prateek.dart:dart-processor:2.0.2"

    // rx java
    compile "io.reactivex:rxjava:1.2.9"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava-async-util:0.21.0"
    compile "com.artemzin.rxjava:proguard-rules:1.2.9.0"
    compile "com.trello:rxlifecycle:1.0"
    compile "com.trello:rxlifecycle-android:1.0"
    compile "com.trello:rxlifecycle-components:1.0"
    compile "com.trello:rxlifecycle-kotlin:1.0"

    compile "com.jakewharton.rxbinding:rxbinding-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-design-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:$VersionRxBinding"

    // bottomsheets
    compile "com.flipboard:bottomsheet-core:1.5.3"

    compile "pl.brightinventions:slf4android:0.1.2"
    compile "com.crashlytics.sdk.android:crashlytics:2.6.7"

    apt "org.immutables:value:$VersionImmutables"
    provided "org.immutables:value:$VersionImmutables"
    provided "org.immutables:gson:$VersionImmutables"

    compile "com.google.dagger:dagger:2.10"
    apt "com.google.dagger:dagger-compiler:2.10"

    // use a newer version of retrolambda
    retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.5.1"

    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.google.code.findbugs:jsr305:3.0.2"
    androidTestCompile "com.android.support:support-annotations:$VersionSupportLibrary"
}

