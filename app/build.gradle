apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

apply from: 'version.gradle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'io.fabric.tools:gradle:1.21.2'
        classpath 'me.tatarka:gradle-retrolambda:3.2.4'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23

        versionCode appVersion
        versionName "1.${(int)(appVersion / 10)}"

        resConfigs "en", "de"

        ndk {
            abiFilters "x86", "x86_64", "armeabi", "armeabi-v7a", "arm64-v8a"
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    signingConfigs {
        open {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        play {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "playstore"
            keyPassword "so very secret"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules-release.pro'
        }
    }

    productFlavors {
        def flavorSigningOpen = signingConfigs.open
        def flavorSigningPlay = signingConfigs.play

        open {
            applicationId "com.pr0gramm.app"
            signingConfig flavorSigningOpen
            buildConfigField "boolean", "IS_PLAYSTORE_RELEASE", "false"
            buildConfigField "boolean", "REQUIRES_UNLOCK_PLUGIN", "false"
        }

        play {
            applicationId "io.github.mopsalarm.pr0gramm.gp"
            signingConfig flavorSigningPlay
            buildConfigField "boolean", "IS_PLAYSTORE_RELEASE", "true"
            buildConfigField "boolean", "REQUIRES_UNLOCK_PLUGIN", "false"
        }

        play2 {
            applicationId "io.github.mopsalarm.pr0gramm.gp2"
            signingConfig flavorSigningPlay
            buildConfigField "boolean", "IS_PLAYSTORE_RELEASE", "true"
            buildConfigField "boolean", "REQUIRES_UNLOCK_PLUGIN", "true"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        def filesToDelete = fileTree(variant.mergeResources.outputDir) {
            // remove timezone data
            include "raw/joda_*"
            exclude "raw/joda_etc_utc"
            exclude "raw/joda_europe_berlin"
            exclude "raw/joda_europe_zurich"
            exclude "raw/joda_zoneinfomap"

            // remove left-to-right drawables
            include "drawable-ldrtl-*/*"
        }

        filesToDelete.each { it.delete() }
    }
}

retrolambda {
    incremental false
}

repositories {
    maven { url "https://maven.fabric.io/public" }
    maven { url "http://bright.github.io/maven-repo/" }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'javax.annotation:jsr250-api:1.0'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.guava:guava:19.0'

    compile 'net.danlew:android.joda:2.9.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'com.squareup.okhttp:okhttp:2.7.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.11'
    compile 'com.github.satyan:sugar:1.4'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.2.4'
    compile 'com.github.tony19:named-regexp:0.2.3'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'com.squareup.sqlbrite:sqlbrite:0.5.0'
    compile "com.infstory:proguard-annotations:1.0.2"
    compile 'net.sf.trove4j:trove4j:3.0.3'


    compile 'com.facebook.stetho:stetho:1.2.0'
    compile 'com.facebook.stetho:stetho-okhttp:1.2.0'

    compile 'com.jakewharton:butterknife:8.0.0-SNAPSHOT'
    apt 'com.jakewharton:butterknife-compiler:8.0.0-SNAPSHOT'

    compile ("com.f2prateek.dart:dart:2.0.0-RC1") {
        // someone fucked up and added this library.
        // see https://github.com/f2prateek/dart/issues/74
        exclude group: "com.squareup", module: "javapoet"
    }
    apt 'com.f2prateek.dart:dart-processor:2.0.0-RC1'

    // rx java
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.0.0'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'

    compile('pl.brightinventions:slf4android:0.1.2@aar') {
        transitive = true
    }

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    apt 'org.immutables:value:2.1.4'
    provided 'org.immutables:value:2.1.4'
    provided 'org.immutables:gson:2.1.4'

    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
}
