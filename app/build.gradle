apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'

apply from: 'version.gradle'

buildscript {
    ext {
        VersionKotlin = "1.1.3-2"
        VersionSupportLibrary = "26.0.0"
        VersionRxBinding = "1.0.1"
        VersionRetrofit = "2.3.0"
        VersionGoogleServices = "11.0.0"
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$VersionKotlin"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        applicationId "com.pr0gramm.app"

        versionCode appVersion
        versionName "1.${(int) (appVersion / 10)}.${(int) (appVersion % 10)}"

        resConfigs "en", "de"

        // vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters "x86", "x86_64", "armeabi", "armeabi-v7a", "arm64-v8a"
        }

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    aaptOptions {
        // See https://www.reddit.com/r/androiddev/comments/47ni7v/if_you_support_older_versions_of_android_there_is/
        additionalParameters "--no-version-vectors"
        cruncherEnabled = false
    }

    sourceSets.main {
        java.srcDirs += 'src/main/kotlin'
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules-debug.pro', getDefaultProguardFile('proguard-android-optimize.txt')
            // testProguardFiles 'proguard-rules-debug.pro', getDefaultProguardFile('proguard-android.txt')
            versionNameSuffix ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules-release.pro', getDefaultProguardFile('proguard-android-optimize.txt')

            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/rxjava.properties'
    }
}

android.applicationVariants.all { variant ->
    variant.mergeResources.doLast {
        def filesToDelete = fileTree(variant.mergeResources.outputDir) {
            // remove timezone data
            include "raw/joda_*"
            exclude "raw/joda_etc_utc"
            exclude "raw/joda_europe_berlin"
            exclude "raw/joda_europe_zurich"
            exclude "raw/joda_zoneinfomap"

            // remove left-to-right drawables
            include "drawable-ldrtl-*/*"
        }

        filesToDelete.each { it.delete() }
    }

    // delete .so-files we dont want in the finished build
    tasks."merge${variant.name.capitalize()}JniLibFolders".doLast {
        def filesToDelete = fileTree("build/") {
            include "**/armeabi/libpl_droidsonroids_gif.so"
            include "**/mips*/*.so"
        }

        filesToDelete.each { it.delete() }
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://maven.fabric.io/public" }
    maven { url "http://dl.bintray.com/amulyakhare/maven" }
    maven { url "http://dl.bintray.com/skoumal/maven" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile project(":api")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$VersionKotlin"
    compile "com.google.code.findbugs:jsr305:3.0.2"

    compile "com.android.support:support-fragment:$VersionSupportLibrary"
    compile "com.android.support:recyclerview-v7:$VersionSupportLibrary"
    compile "com.android.support:design:$VersionSupportLibrary"
    compile "com.android.support:customtabs:$VersionSupportLibrary"
    compile "com.android.support:palette-v7:$VersionSupportLibrary"
    compile "com.android.support:support-annotations:$VersionSupportLibrary"
    compile "com.android.support:cardview-v7:$VersionSupportLibrary"
    compile("com.android.support:appcompat-v7:$VersionSupportLibrary") {
        exclude(module: "support-media-compat")
        exclude(module: "support-core-utils")
    }

    compile("com.google.guava:guava:22.0-android") {
        force = true
    }

    compile "com.google.android.gms:play-services-analytics:$VersionGoogleServices"
    compile "com.google.android.gms:play-services-ads:$VersionGoogleServices"

    compile "net.danlew:android.joda:2.9.9"
    compile "com.squareup.retrofit2:retrofit:$VersionRetrofit"
    compile "com.squareup.retrofit2:converter-gson:$VersionRetrofit"
    compile "com.squareup.retrofit2:adapter-rxjava:$VersionRetrofit"
    compile "com.squareup.okhttp3:okhttp:3.8.1"
    compile "com.squareup.picasso:picasso:2.5.2"
    compile "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"
    compile "pl.droidsonroids.gif:android-gif-drawable:1.2.7"
    compile "com.github.tony19:named-regexp:0.2.5"
    compile "com.pnikosis:materialish-progress:1.7"
    compile "com.amulyakhare:com.amulyakhare.textdrawable:1.0.1"
    compile "com.squareup.sqlbrite:sqlbrite:1.1.2"
    compile "com.infstory:proguard-annotations:1.0.2"
    compile "net.sf.trove4j:trove4j:3.0.3"
    compile "com.davemorrissey.labs:subsampling-scale-image-view:3.6.0"
    compile "org.nanohttpd:nanohttpd:2.3.1"
    compile "com.indeed:java-dogstatsd-client:2.0.16"
    compile "me.leolin:ShortcutBadger:1.1.17@aar"
    compile "it.sephiroth.android.exif:android-exif-extended:1.0.6"


    compile 'com.github.evernote:android-job:v1.1.11'

    compile "com.github.apl-devs:appintro:v4.2.2"

    compile 'com.google.android.exoplayer:exoplayer-core:r2.4.4'

    // forked dependencies.
    compile "com.github.mopsalarm:FinestWebView-Android:aef2e77ca9"

    debugCompile "com.facebook.stetho:stetho:1.5.0"
    debugCompile "com.facebook.stetho:stetho-okhttp3:1.5.0"

    // rx java
    compile "io.reactivex:rxjava:1.3.0"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava-async-util:0.21.0"
    compile 'io.reactivex:rxkotlin:1.0.0'
    compile "com.artemzin.rxjava:proguard-rules:1.3.0.0"
    compile "com.trello:rxlifecycle:1.0"
    compile "com.trello:rxlifecycle-android:1.0"
    compile "com.trello:rxlifecycle-components:1.0"
    compile "com.trello:rxlifecycle-kotlin:1.0"

    compile "com.jakewharton.rxbinding:rxbinding-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-design-kotlin:$VersionRxBinding"
    compile "com.jakewharton.rxbinding:rxbinding-recyclerview-v7-kotlin:$VersionRxBinding"

    // bottomsheets
    compile "com.flipboard:bottomsheet-core:1.5.3"

    compile "com.github.bright:slf4android:0.1.3"
    compile "com.crashlytics.sdk.android:crashlytics:2.6.8"

    // dependency injection using kotlin.
    compile 'com.github.salomonbrys.kodein:kodein:4.1.0'
    compile 'com.github.salomonbrys.kodein:kodein-android:4.1.0'

    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    testCompile 'junit:junit:4.12'
    testCompile 'com.natpryce:hamkrest:1.4.1.0'
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$VersionKotlin"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$VersionKotlin"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$VersionKotlin"
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
}
